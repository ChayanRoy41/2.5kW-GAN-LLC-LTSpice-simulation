* Created in LTspice Version XVII                               *
*                                            					*
* GaN Systems Inc. Power Transistors                            *
* LTSpice Library for GaN Transistors                           *
* Version 3.1                                         			*
*                                                               *
*****************************************************************
*****************************************************************
*                                                               *
* Models provided by GaN Systems Inc. are not warranted by      *
* GaN Systems Inc. as fully representing all of the 			*
* specifications and operating characteristics of the 			*
* semiconductor product to which the model relates. The model 	*
* describes the characteristics of a typical device.			*
* In all cases, the current data sheet information for a given  *
* device is the final design guideline and the only actual      *
* performance specification.                                    *
* Although models can be a useful tool in evaluating the devices*
* performance, they cannot model exact device performance under *
* all conditions, nor are they intended to replace bread-       *
* boarding for final verification. GaN Systems Inc. therefore   *
* does not assume any liability arising from their use.         *
* GaN Systems Inc. reserves the right to change models without  *
* prior notice.                                                 *
*                                                               *
* This library contains models of the following GaN Systems     *
* Inc. transistors:                                             *
*                                                               *
* GS-065-004-1-L                                              	*
*****************************************************************
*Level 1. Focus on simulation speed.
*
*
.subckt GaN_LTspice_GS-065-004-1-L_L1V3P1 gatein drainin sourcein source_S
*
*
.param 	sf = 0.3668
.param rTC=-0.004	gan_res={125.56e-3}	metal_res={ (6.3e-3/sf) + (4.8e-3/sf) }	gtc=2.893	sh_s = 0.05263	sh_d = 0.94376
.param  cur=0.0322	x0_0=1.1	x0_1=0.3 	x0_2=1.0 	thr = 1.7 	itc=0.147	atc=30.83459808
*
*
rd drainin drain {sh_d*(metal_res*(1-1*rTc*(Temp-25)) + gan_res*PWR((Temp+273)/298,gtc)) / sf }
rs sourcein source {sh_s * (metal_res*(1-1*rTc*(Temp-25)) + gan_res*PWR((Temp+273)/298,gtc)) / sf }
RSS source_S source {0.035}
rg gatein gate {1.4+0.065}
*
*
Rcsdconv drain source {4000Meg}
Rcgsconv gate source {4000Meg}
Rcgdconv gate drain {4000Meg}
*
*
bswitch drain2 source2 I = (if(v(drain2,source2)>0,
+   (cur*(-(Temp-25)*itc+atc)*log(1.0+exp(26*(v(gate,source2)-thr)))*
+	v(drain2,source2)/(1 + max(x0_0+x0_1*(v(gate,source2)+x0_2),0.2)*v(drain2,source2))),

+   (-cur*(-(Temp-25)*itc+atc)*log(1.0+exp(26*(v(gate,drain2)-thr)))*
+	v(source2, drain2)/(1 + max(x0_0+x0_1*(v(gate,drain2)+x0_2),0.2)*v(source2,drain2))))) * sf
*
*
R_drain2 drain2 drain {(1e-4)}
R_source2 source2 source {(1e-4)}
*
*
C_GS	gate source  	{6.8e-11 * sf} 
C_GS1	gate source		Q = (	-2.65e-11*(1-1./(1+exp(0.07*(-v(drain, source)+20))))
+								-0.37e-10*(-0.682+0.66/(1+exp(0.11*(-v(drain, source)+.01))))) * x * sf
*
*
C_GS2	gate source		Q = 3.15e-011*log(1+exp(8*(x-0.9))) * sf
*
*
C_GD	gate drain		{0.3e-012 * sf}
C_GD1	gate drain		Q = 375e-12*log(1+exp(0.07*(x+20))) * sf
*
*
C_SD	source drain	{2e-011 * sf}
C_SD1	source drain	Q =  0.85e-9*log(1+exp(0.085*(x+63))) * sf 
+							+1.45e-9*log(1+exp(0.025*(x+180)))  * sf
+							-2.65e-11*(1-1./(1+exp(0.07*(x+20))) * sf)
*
*
.ends
*$